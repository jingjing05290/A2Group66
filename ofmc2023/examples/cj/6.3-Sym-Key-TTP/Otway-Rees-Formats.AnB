Protocol: OtwayRees # Verified  --untyped

# The type-flaw attack in the untyped model is corrected by the use of formats

# in the typed model, there is a relatively stupid replay attack if A=B is allowed
# we exclude this by A!=B

Types: Agent A,B,s;
       Number NA,NB,M;
       Format f1,f2;
       Symmetric_key KAB;

Knowledge: A: A,B,sk(A,s);
	   B: B,A,sk(B,s);
 	   s: A,B,sk(A,s),sk(B,s)
where A!=B

Actions:
A->B: M,A,B,{|f1(NA,M,A,B)|}sk(A,s)
B->s: M,A,B,{|f1(NA,M,A,B)|}sk(A,s),{|f1(NB,M,A,B)|}sk(B,s)
s->B: M,{|f2(NA,KAB)|}sk(A,s),{|f2(NB,KAB)|}sk(B,s)
B->A: M,{|f2(NA,KAB)|}sk(A,s)

Goals:
B authenticates s on KAB
A authenticates s on KAB
KAB secret between A,B,s
